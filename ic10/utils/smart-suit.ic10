define TEMP_MIN 273
define TEMP_MAX 312
define PRES_MIN 60
define PRES_MAX 150
alias suit db
alias helmet d0
alias notificationTime r9
alias prevStormMinutes r10
alias tempLow r11
alias tempHigh r12
alias presLow r13
alias presHigh r14
alias atmosGood r15
clr db # clear stack

start:
yield
jal checkAtmos
jal handleBadAtmos
jal handleGoodAtmos
jal checkStorm
jal handleNotifications
jal handleSoundAlertClear
j start

checkAtmos:
l r0 suit PressureExternal
slt presLow r0 PRES_MIN
sgt presHigh r0 PRES_MAX
l r0 suit TemperatureExternal
slt tempLow r0 TEMP_MIN
sgt tempHigh r0 TEMP_MAX
move r0 0
or r0 r0 presLow
or r0 r0 presHigh
or r0 r0 tempLow
or r0 r0 tempHigh
seqz atmosGood r0
j ra
handleBadAtmos:
bnez atmosGood ra
s suit On 1
s suit AirRelease 1
s suit Filtration 1
l r0 helmet Open
beqz r0 ra
s helmet Open 0
breqz presLow 3
push Sound.PressureLow
push 6
breqz presHigh 3
push Sound.PressureHigh
push 6
breqz tempLow 3
push Sound.TemperatureLow
push 6
breqz tempHigh 3
push Sound.TemperatureHigh
push 6
j ra
handleGoodAtmos:
beqz atmosGood ra
s suit On 0
s suit AirRelease 0
s suit Filtration 0
s helmet Open 1
j ra

checkStorm:
move r1 prevStormMinutes # Save off old for later
l r0 suit Setting
div r0 r0 60
ceil r0 r0
move prevStormMinutes r0 # Update prev storm time
beq r0 r1 ra # Only notify once per minute
brne r0 5 3
push Sound.Five
push 1
brne r0 4 3
push Sound.Four
push 1
brne r0 3 3
push Sound.Three
push 1
brne r0 2 3
push Sound.Two
push 1
brne r0 1 3
push Sound.One
push 1
push Sound.StormIncoming
push 4
j ra

handleNotifications:
blez sp ra # Queue non-empty
bgtz notificationTime ra # Sound playing
pop notificationTime
pop r0
s suit SoundAlert r0
j ra
handleSoundAlertClear:
brlez notificationTime 3
sub notificationTime notificationTime 1
j ra
s suit SoundAlert Sound.None
j ra

