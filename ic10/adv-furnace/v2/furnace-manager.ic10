alias furnace d0
alias memSelection d1
alias switchEject d2
alias tankBuffer d3

define smoothing 0.25

alias lastInput r8
alias lastOutput r9
alias inputMult r10
alias targetTempMin r11
alias targetTempMax r12
alias targetPresMin r13
alias targetPresMax r14
alias targetPresAvg r15

s furnace SettingOutput 0
s furnace SettingInput 0

loop:
yield

# Over-pressure detection
l r0 furnace Pressure
brlt r0 60000 4
s furnace SettingOutput 100
s furnace SettingInput 0
j loop

l r0 memSelection Setting
mul r0 r0 11
add sp r0 15
pop targetPresMax
sub targetPresMax targetPresMax 100
pop targetPresMin
add targetPresMin targetPresMin 100
pop targetTempMax
sub targetTempMax targetTempMax 100
pop targetTempMin
add targetTempMin targetTempMin 100
add targetPresAvg targetPresMin targetPresMax
div targetPresAvg targetPresAvg 2

l r0 switchEject Setting
s furnace Open r0

# Manage SettingInput & SettingOutput
move r2 50
move r3 50

l r0 furnace Pressure
bgt r0 targetPresAvg decrease_pressure
blt r0 targetPresAvg increase_pressure

increase_pressure:
sub r1 targetPresAvg r0
div r1 r1 100
mul r1 r1 r1
min r1 r1 50
add r2 r2 r1
sub r3 r3 r1
j done

decrease_pressure:
sub r1 r0 targetPresAvg
div r1 r1 100
mul r1 r1 r1
min r1 r1 50
sub r2 r2 r1
add r3 r3 r1
done:

sub r2 r2 lastInput
mul r2 r2 smoothing
add r2 lastInput r2

sub r3 r3 lastOutput
mul r3 r3 smoothing
add r3 lastOutput r3

s furnace SettingInput r2
s furnace SettingOutput r3

move lastInput r2
move lastOutput r3

j loop